import net.fabricmc.loom.task.RemapJar

plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.3.31"
    id "idea"
    id "fabric-loom" version "0.2.2-SNAPSHOT"
    id "com.github.ben-manes.versions" version "0.21.0"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "5.0.0"
    id "com.matthewprenger.cursegradle" version "1.2.0"
}

repositories {
    maven {
        url = "http://maven.fabricmc.net/"
        name = "Fabric"
    }
    maven {
        url = "https://kotlin.bintray.com/kotlinx"
        name = "Kotlin X"
    }
    maven { url = "https://dl.bintray.com/magneticflux/maven" }
    jcenter()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

minecraft {
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}"
    modCompile "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modCompile "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modCompile "net.fabricmc:fabric-language-kotlin:1.3.31+build.2"
    include "net.fabricmc:fabric-language-kotlin:1.3.31+build.2"

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.
    modCompile "io.github.prospector.modmenu:ModMenu:1.5.3-84"

    shadow("com.skaggsm:java-mumble-link:0.2.2") {
        exclude group: "net.java.dev.jna"
    }
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

jar {
    from "LICENSE"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

shadowJar {
    configurations = [project.configurations.shadow]
    relocate "com.skaggsm.jmumblelink", "com.skaggsm.mumblelinkmod.shadowed.jmumblelink"
    relocate "com.skaggsm.sharedmemory", "com.skaggsm.mumblelinkmod.shadowed.sharedmemory"
}

task remapShadowJar(type: RemapJar, dependsOn: shadowJar) {
    delegate.jar = shadowJar.archivePath
}

tasks.remapShadowJar.dependsOn tasks.shadowJar
tasks.assemble.dependsOn tasks.remapShadowJar

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapShadowJar.jar) {
                builtBy remapShadowJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}

curseforge {
    // Stored in ~/.gradle/gradle.properties
    apiKey = project.curseApiKey

    project {
        id = "321669"
        releaseType = "beta"
        addGameVersion "1.14-Snapshot"
        addGameVersion "1.14"
        mainArtifact(remapShadowJar.jar) {
            displayName = "$archives_base_name-$project.version"
            relations {
                requiredDependency "fabric"
                embeddedLibrary "fabric-language-kotlin"
            }
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

afterEvaluate {
    // CurseGradle generates tasks in afterEvaluate for each project
    // There isn't really any other way to make it depend on a task unless it is an AbstractArchiveTask
    tasks.curseforge321669.dependsOn remapShadowJar
}
